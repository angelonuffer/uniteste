processo = https://cdn.jsdelivr.net/gh/angelonuffer/processo@38812fea4b372b27b3b5038fc2b17155a6a8e980/cÃ³digo/0

compara_listas = { lista_1 lista_2 } =>
  | lista_1[.] != lista_2[.] = 0
  | lista_1[.] == 0 = 1
  | lista_1[0] != lista_2[0] = 0
  | compara_listas({
    lista_1: lista_1[1:]
    lista_2: lista_2[1:]
  })

escrever_e_exibir = { passaram mensagens mensagens_escritas } =>
  | mensagens[.] == mensagens_escritas[.] = processo.nodejs([
    ...mensagens_escritas
    processo.escreva(passaram + "/" + mensagens[.] + " testes passaram.")
    passaram == mensagens[.] ?
      processo.saia(0)
    :
      processo.saia(1)
  ])
  | escrever_e_exibir({
    passaram
    mensagens
    mensagens_escritas: [
      ...mensagens_escritas
      processo.escreva(mensagens[mensagens_escritas[.]])
    ]
  })

incluir_nome = nome => mensagens =>
  | mensagens[.] == 0 = []
  | mensagens[.] == 1 = [
    [
      nome
      " | "
      mensagens[0]
    ] * ""
  ]
  | [
    ...(incluir_nome(nome))(mensagens[0:1])
    ...(incluir_nome(nome))(mensagens[1:])
  ]

descrever = { nome testes } =>
  | testes[.] == 0 = {
    passaram: 0
    mensagens: []
  }
  | testes[.] == 1 = {
    passaram: testes[0].passaram
    mensagens: (incluir_nome(nome))(testes[0].mensagens)
  }
  | descrever({
    nome
    testes: [
      {
        passaram: testes[0].passaram + testes[1].passaram
        mensagens: [
          ...testes[0].mensagens
          ...testes[1].mensagens
        ]
      }
      ...testes[2:]
    ]
  })

{
  descrever
  iguais: args => (
    passaram = args[0] == args[1] ? 1 : 0
    {
      passaram
      mensagens: [
        passaram == 1 ? ["ok"] : [
          "Esperava que "
          "" + args[0]
          " fosse igual a "
          "" + args[1]
          "."
        ]
      ]
    }
  )
  listas_iguais: args => (
    passaram = compara_listas({
      lista_1: args[0]
      lista_2: args[1]
    }) ? 1 : 0
    {
      passaram
      mensagens: [
        passaram == 1 ? ["ok"] : [
          "Esperava que ["
          args[0] * ", "
          "] fosse igual a ["
          args[1] * ", "
          "]."
        ]
      ]
    }
  )
  exibir: { passaram mensagens } => escrever_e_exibir({
    passaram
    mensagens
    mensagens_escritas: []
  })
}